# Function Calls

Once more, let's look at a code example:

``` ARM
length:
    mov x2, -1
length_rec:
    add x2, x2, 1
    ldrb w1, [x0], 1    // Load byte from mem at addr in x0 and increment it by 1
    cbnz w1, length_rec
    mov x0, x2
    ret
    
.global _start
_start:
    adr x0, txt         // argument for length. x0 is the parameter by convention
    bl length           // save pc+4 in lr (x30), transfer control to length
    mov x2, x0
    mov x0, 1
    adr x1, txt
    mov x8, 64
    svc 0
    mov x0, 0
    mov x8, 93
    svc 0

txt: .asciz "goodbye, cruel world!\n" //.asciz is a zero-terminated string
```

`bl` branches to another address and saves `pc+4` to `x30`, otherwise known as the `link register (lr)`. This way, we know the position of the next command once we're done executing the function.

`ldrb` loads the byte into `w1` which represents the lower 32 bits of `x1`.

`cbnz` compares and branches on non zero. So if `w1 != 0`, we loop in `length_rec`. 

`ret` is identical to `mov pc, lr`.

> *If a function wants to call another funtion. It needs to save its own current link register before making the call.*

## Calling Conventions

**Calling a function?**

- `bl *address*`
- `blr *register*`

**Where do we put arguments?**

- `x0,x1,...x7`, then stack

**Where do results go?**

- `x0`
- More results, we go from `x1-x7`, then stack.
- indirect: `x8`

**Where's the return address?**

- `lr (x30)`

**Who saves registers?**

- Caller: `x0-x18`
- Callee: `x19-x30`

**Where is the Stack?**

- `sp`, aligned on the 16B boundary (meaning it always has to be a multiple of 16)
- Grows towards *lower addresses*

## Call Frame (Activation Record)
- A Function's local storage that is *allocated* on call and *removed* on return.
- Often arranged as a *Call Stack*.
- The *Frame Pointer* is the start of the frame
- The *Stack Pointer* is the top of the stack

The frame is where all important stuff is saved.


## A new example:

``` ARM
print:
    sub sp, sp, 16              // Make space on the stack
    str lr, [sp, 8]             // Store the lr to the stack
    str x0, [sp]                // Store x0 to the stack
    bl length                   // We call length
    mov x2, x0                  // Put length into x2
    mov x0, 1                   // 1 for stdout
    ldr x1, [sp]                // Load our argument for print from stack
    mov x8, 64                  // syscall is write
    svc 0                       // print to stdout
    ldr lr, [sp, 8]             // Restore the lr
    add sp, sp, 16              // Remove space from the stack
    ret                         // return to callee

_start:
    adr x0, hi
    bl print
    adr x0, bye
    bl print
    mov x0, 0
    mov x8, 93
    svc 0

hi: .asciz "hello, brave new world!\n"
bye: .asciz "goodbye, cruel world!\n"
```
