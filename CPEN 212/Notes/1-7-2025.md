# CPEN 212: Procedure Call Abstraction

## Introductions:
- There will be a quiz every class, based on the content from the previous class
- High probability there will be 6 labs
- HOLY SHIT WE DON'T ACTUALLY HAVE TO GO TO THE LAB
- apparently we're not supposed to plagarize idk man sounds weird

## The class begins:

![Alt text](images\image.png)

### The Pipeline:
We learnt last semester how the CPU takes instructions and spits results. But obviously, there is a lot more than that to a computer:

**Library APIs** allow us to easily access common functions. They're simply packets of instructions to the CPU that are common, like `printf` and `malloc`, for example.

**Operating System Kernels** like Windows and Linux serve one major purpose: To ensure the safe execution multiple programs. For example, the OS would prevent two programs from sayâ€”modifying each other's memory. The **Hardware** is also important to isolating the the OS from other programs.

> **If Libraries and the OS are both important, why seperate them?**
> 
> Well, technically, we don't need to! This is a design by convention. 
> It does have benefits, like for example, a user can have an OS that fits most needs, but can also download extra libraries without reinstalling an entire OS.
> Similarly, it might be incovenient to use a barebones OS where even the GUI is a library that needs seperate, standalone installation.

**Interrupts** are unexpected changes to instruction calls. A user making a keyboard input, the sudden arrival of a network packet, these are all cases where the CPU needs to change program execution based on random information. Typically, hardware like Network Cards and IO devices are the ones sending interrupts.

**Signals** are how the OS communicates information to Libraries, like interrupts! When we're talking from Libraries to the Application, this is called a **Callback**.

## Starting a Program

Like usual, we'll take a look at an example:

``` ARM
    .text
    .global _start
_start:
    mov x0, 42
    mov x8, 93
    svc 0
```

To run this, we have to first compile and run it with:
```
as -o foo.o foo.s && ld -o foo foo.o ./foo
```

Here, the line `as -o foo.o foo.s` calls the assembler to make a `foo.o` object file.

On the other hand, `ld -o foo foo.o ./foo` tells the linker to combine the object files into a `foo` program.

On `Unix`, we can type `echo $?` during program execution to show the return status of the program. If we did it right now, we would get the output: `42`. Hey! That number is in the code! Let's go over the code again

``` ARM
    .text               // Tells the assembler/linker that code follows
    .global _start      // Makes _start visible to the linker
_start:                 // Tells the assembler/linker where to begin execution
    mov x0, 42          // Puts value in x0 (exit code)
    mov x8, 93          // Puts value in x8 (syscall #)
    svc 0               // system call: Invokes the OS (syscall 93 is exit)
```

Since we put `93` into the syscall number, we invoke the OS to end the program. Bit weird to write a program that just kills itself, but sure, let's go with it. `x8` is a calling convention for the `syscall #`. There's nothing special about this number! Meanwhile, in `x0`, we put `42`. This too, is just a calling convention for the exit code. Lo and behold, we find that `echo $` returns `42`.

## Moving on to another code example...

Here's an example that prints a string in `ARM`!

``` ARM
.text

.global _start
_start:
    mov x8, 64          // syscall is now write (64)
    mov x0, 1           // destination: 1 is stdout (standard out)
    adr x1, txt         // puts PC-relative address in x1, (adds offset to PC)
    mov x2, len         // puts the length of the string in x2
    svc 0               // System call for write (relies on arguments from x0, x1 x2)
    mov x8, 93          // syscall is 93 (exit)
    mov x0, 42          // exit code is now 42
    svc 0               // System call: This time to exit

txt: .ascii "goodbye, cruel world!\n"
len = . - txt           // Length is computed by subtracting the address of the string
                        // from the current address (.), which makes sense!
```

We'll also take a look at this code, but for `x86`:

``` ARM
section .text

global _start
_start:
    mov rax, 1
    mov rdi, 1
    mov rsi, txt
    mov rdx, len
    syscall
    mov rax, 60
    mov rdi, 42
    syscall

txt db "goodbye, cruel world!", 10
len equ $ - txt
```

Unsurprisingly, most people think very alike, so it's not a big surprise that the x86 version looks like the ARM one.